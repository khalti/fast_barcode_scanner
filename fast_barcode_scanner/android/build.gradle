version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.20'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 34

    namespace "com.jhoogstraat.fast_barcode_scanner"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
    }
}

def usePlayServiceMlkit = false

if (project.hasProperty('dart-defines')) {
    def dartEnvVars = project.property('dart-defines').split(',').collectEntries {
        def splits = new String(it.decodeBase64()).split('=')
        [splits.first(), splits.last()]
    }
    if (dartEnvVars.containsKey('usePlayServiceMlkit')) {
        usePlayServiceMlkit = dartEnvVars['usePlayServiceMlkit'] == 'true'
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def camerax_version = "1.3.3"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    
    if (usePlayServiceMlkit) {
        implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.0'
    } else {
        implementation "com.google.mlkit:barcode-scanning:17.2.0"
    }
}
